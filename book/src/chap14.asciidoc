[[chap14]]
== Archivos

Este capítulo presenta la idea de programas "persistentes" que mantienen los datos en almacenamiento permanente, y muestra cómo usar diferentes tipos de almacenamiento permanente, tales como archivos y bases de datos..

=== Persistencia

La mayoría de los programas que hemos visto hasta ahora han sido transitorios, es decir se ejecutan por un corto tiempo y generan una de salida, pero cuando terminan, sus datos desaparecen. Si ejecuta el programa nuevamente, este comienza de cero.
(((transient)))

Otros programas son _persistentes_; se ejecutan durante un largo período de tiempo (o todo el tiempo), mantienen al menos parte de sus datos en almacenamiento permanente (en un disco duro, por ejemplo), y si se apagan y vuelven a comenzar, retoman donde lo dejaron.
(((persistent)))

Ejemplos de programas persistentes son los sistemas operativos, que se ejecutan siempre que una computadora esté encendida, y los servidores web, que se ejecutan todo el tiempo, esperando que lleguen solicitudes a la red.

Una de las formas más simples para que los programas mantengan sus datos es leyendo y escribiendo _archivos de texto_. Ya hemos visto programas que leen archivos de texto; en este capítulo veremos programas que los escriben.

Otra alternativa es almacenar el estado del programa en una base de datos. En este capítulo también se presentará cómo usar una base de datos simple.

[[reading_and_writing]]
=== Lectura y Escritura

Un archivo de texto es una secuencia de caracteres almacenados en un medio permanente, como un disco duro o una memoria flash. Ya vimos cómo abrir y leer un archivo en <<reading_word_lists>>.

Para escribir un archivo, debe abrirlo con el modo +"w"+ como segundo parámetro:
(((open)))

[source,@julia-repl-test chap14]
----
julia> fout = open("salida.txt", "w")
IOStream(<file salida.txt>)
----

Si el archivo ya existe, abrirlo en modo de escritura borra los datos antiguos y comienza de nuevo, ¡así que tenga cuidado!. Si el archivo no existe, se crea uno nuevo. +open+ devuelve un objeto de archivo y la función +write + coloca datos en el archivo.
(((write)))((("function", "Base", "write", see="write")))

[source,@julia-repl-test chap14]
----
julia> linea1 = "El Cid convoca a sus vasallos;\n";

julia> write(fout, linea1)
31
----

El valor de retorno es el número de caracteres que se escribieron. El objeto de archivo lleva registro de dónde quedó por última vez, por lo que si llama +write+ nuevamente, agrega nuevos datos al final del archivo.

[source,@julia-repl-test chap14]
----
julia> linea2 = "éstos se destierran con él.\n";

julia> write(fout, linea2)
30
----

Cuando termine de escribir, debe cerrar el archivo.
(((close)))

[source,@julia-repl-test chap14]
----
julia> close(fout)

----

Si no cierra el archivo, se cierra cuando finaliza el programa.


=== Formateo

El argumento de write tiene que ser una cadena, por lo que si queremos poner otros valores en un archivo, tenemos que convertirlos en cadenas. La forma más fácil de hacerlo es con la función +string+, o con interpolación de cadenas:
(((string)))(((string interpolation)))

[source,@julia-repl-test chap14]
----
julia> fout = open("output.txt", "w")
IOStream(<file output.txt>)
julia> write(fout, string(150))
3
----

Otra alternativa es utilizar la familia de funciones de +print(ln)+.
(((print)))(((println)))

[source,@julia-repl-test chap14]
----
julia> camellos = 42
42
julia> println(fout, "He visto $camellos camellos.")

----

[TIP]
====
Una alternativa más potente es la macro +@printf+, que imprime utilizando cadenas de especificación de formato al más puro estilo C, lo cual puede leer en https://docs.julialang.org/en/v1/stdlib/Printf/
(((@printf)))((("macro", "Printf", "@printf", see="@printf")))(((formatting)))
====


=== Filenames and Paths

Files are organized into _directories_ (also called “folders”). Every running program has a “current directory”, which is the default directory for most operations. For example, when you open a file for reading, Julia looks for it in the current directory.
(((directory)))((("folder", see="directory")))

The function +pwd+ returns the name of the current directory:
(((pwd)))((("function", "Base", "pwd", see="pwd")))

[source,jlcon]
----
julia> cwd = pwd()
"/home/ben"
----

+cwd+ stands for “current working directory”. The result in this example is +/home/ben+, which is the home directory of a user named +ben+.

A string like +"/home/ben"+ that identifies a file or directory is called a _path_.
(((path)))

A simple filename, like +memo.txt+ is also considered a path, but it is a _relative path_ because it relates to the current directory. If the current directory is +/home/ben+, the filename +memo.txt+ would refer to +/home/ben/memo.txt+.
(((relative path)))((("path", "relative", see="relative path")))

A path that begins with +/+ does not depend on the current directory; it is called an _absolute path_. To find the absolute path to a file, you can use +abspath+:
(((absolute path)))((("path", "absolute", see="absolute path")))(((abspath)))((("function", "Base", "abspath", see="abspath")))

[source,jlcon]
----
julia> abspath("memo.txt")
"/home/ben/memo.txt"
----

Julia provides other functions for working with filenames and paths. For example, +ispath+ checks whether a file or directory exists:
(((ispath)))((("function", "Base", "ispath", see="ispath")))

[source,jlcon]
----
julia> ispath("memo.txt")
true
----

If it exists, +isdir+ checks whether it’s a directory:
(((isdir)))((("function", "Base", "isdir", see="isdir")))

[source,jlcon]
----
julia> isdir("memo.txt")
false
julia> isdir("/home/ben")
true
----

Similarly, +isfile+ checks whether it’s a file.
(((isfile)))((("function", "Base", "isfile", see="isfile")))

+readdir+ returns an array of the files (and other directories) in the given directory:
(((readdir)))((("function", "Base", "readdir", see="readdir")))

[source,jlcon]
----
julia> readdir(cwd)
3-element Array{String,1}:
 "memo.txt"
 "music"
 "photos"
----

To demonstrate these functions, the following example “walks” through a directory, prints the names of all the files, and calls itself recursively on all the directories.
(((walk)))((("function", "programmer-defined", "walk", see="walk")))

[source,@julia-setup chap14]
----
function walk(dirname)
    for name in readdir(dirname)
        path = joinpath(dirname, name)
        if isfile(path)
            println(path)
        else
            walk(path)
        end
    end
end
----

+joinpath+ takes a directory and a file name and joins them into a complete path.
(((joinpath)))((("function", "Base", "joinpath", see="joinpath")))

[TIP]
====
Julia provides a function called +walkdir+ (see https://docs.julialang.org/en/v1/base/file/#Base.Filesystem.walkdir) that is similar to this one but more versatile. As an exercise, read the documentation and use it to print the names of the files in a given directory and its subdirectories.
(((walkdir)))((("function", "Base", "walkdir", see="walkdir")))
====


[[catching_exceptions]]
=== Catching Exceptions

A lot of things can go wrong when you try to read and write files. If you try to open a file that doesn’t exist, you get a +SystemError+:
(((SystemError)))((("error", "Base", "SystemError", see="SystemError")))

[source,@julia-repl-test]
----
julia> fin = open("bad_file")
ERROR: SystemError: opening file "bad_file": No such file or directory
----

Also, if you try to open a file you don’t have permission to access,
you get a "Permission denied" system error.

To avoid these errors, you could use functions like +ispath+ and +isfile+, but it would take a lot of time and code to check all the possibilities.

It is easier to go ahead and try—to deal with problems as they happen—which is exactly what the +try+ statement does. The syntax is similar to an +if+ statement:
(((try statement)))((("statement", "try", see="try statement")))(((try)))((("keyword", "try", see="try")))(((catch)))((("keyword", "catch", see="catch")))(((end)))

[source,julia]
----
try
    fin = open("bad_file.txt")
catch exc
    println("Something went wrong: $exc")
end
----

Julia starts by executing the +try+ clause. If all goes well, it skips the +catch+ clause and proceeds. If an exception occurs, it jumps out of the +try+ clause and runs the +catch+ clause.

Handling an exception with a +try+ statement is called _catching_ an exception. In this example, the except clause prints an error message that is not very helpful. In general, catching an exception gives you a chance to fix the problem, or try again, or at least end the program gracefully.
(((catching an exception)))

In code that performs state changes or uses resources like files, there is typically clean-up work (such as closing files) that needs to be done when the code is finished. Exceptions potentially complicate this task, since they can cause a block of code to exit before reaching its normal end. The +finally+ keyword provides a way to run some code when a given block of code exits, regardless of how it exits:
(((finally)))((("keyword", "finally", see="finally")))

[source,julia]
----
f = open("output.txt")
try
    line = readline(f)
    println(line)
finally
    close(f)
end
----

The function +close+ will always be executed.

[[databases]]
=== Databases

A _database_ is a file that is organized for storing data. Many databases are organized like a dictionary in the sense that they map from keys to values. The biggest difference between a database and a dictionary is that the database is on disk (or other permanent storage), so it persists after the program ends.
(((database)))

PiensaEnJulia provides an interface to +GDBM+ (GNU dbm) for creating and updating database files. As an example, I’ll create a database that contains captions for image files.
(((GDBM)))

Opening a database is similar to opening other files:
(((DBM)))((("type", "PiensaEnJulia", "DBM", see="DBM")))

[source,@julia-repl-test chap14]
----
julia> using PiensaEnJulia

julia> db = DBM("captions", "c")
DBM(<captions>)
----

The mode "c" means that the database should be created if it doesn’t already exist. The result is a database object that can be used (for most operations) like a dictionary.

When you create a new item, +GDBM+ updates the database file:
(((bracket operator)))

[source,@julia-repl-test chap14]
----
julia> db["cleese.png"] = "Photo of John Cleese."
"Photo of John Cleese."
----

When you access one of the items, +GDBM+ reads the file:

[source,@julia-repl-test chap14]
----
julia> db["cleese.png"]
"Photo of John Cleese."
----

If you make another assignment to an existing key, +GDBM+ replaces the old value:
(((assignment)))

[source,@julia-repl-test chap14]
----
julia> db["cleese.png"] = "Photo of John Cleese doing a silly walk."
"Photo of John Cleese doing a silly walk."
julia> db["cleese.png"]
"Photo of John Cleese doing a silly walk."
----

Some functions having a dictionary as argument, like +keys+ and +values+, don’t work with database objects. But iteration with a +for+ loop works:
(((for statement)))(((iteration)))

[source,julia]
----
for (key, value) in db
    println(key, ": ", value)
end
----

As with other files, you should close the database when you are done:
(((close)))

[source,@julia-repl-test chap14]
----
julia> close(db)

----


=== Serialization

A limitation of +GDBM+ is that the keys and the values have to be strings or byte arrays. If you try to use any other type, you get an error.

The functions +serialize+ and +deserialize+ can help. They translate almost any type of object into a byte array (an iobuffer) suitable for storage in a database, and then translates byte arrays back into objects:
(((Serialization)))((("module", "Serialization", see="Serialization")))(((serialize)))((("function", "Serialization", "serialize", see="serialize)))(((IOBuffer)))((("type", "Base", "IOBuffer", see="IOBuffer")))(((take!)))((("function", "Base", "take!", see="take!")))

[source,@julia-repl-test chap14]
----
julia> using Serialization

julia> io = IOBuffer();

julia> t = [1, 2, 3];

julia> serialize(io, t)
24
julia> print(take!(io))
UInt8[0x37, 0x4a, 0x4c, 0x08, 0x04, 0x00, 0x00, 0x00, 0x15, 0x00, 0x08, 0xe2, 0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x03, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00]
----

The format isn’t obvious to human readers; it is meant to be easy for Julia to interpret. +deserialize+ reconstitutes the object:
(((deserialize)))((("function", "Serialization", "deserialize", see="deserialize)))

[source,@julia-repl-test chap14]
----
julia> io = IOBuffer();

julia> t1 = [1, 2, 3];

julia> serialize(io, t1)
24
julia> s = take!(io);

julia> t2 = deserialize(IOBuffer(s));

julia> print(t2)
[1, 2, 3]
----

+serialize+ and +deserialize+ write to and read from a iobuffer object which represents an in-memory I/O stream. The function +take!+ fetches the contents of the iobuffer as a byte array and resets the iobuffer to its initial state.

Although the new object has the same value as the old, it is not (in general) the same object:

[source,@julia-repl-test chap14]
----
julia> t1 == t2
true
julia> t1 ≡ t2
false
----

In other words, serialization and then deserialization has the same effect as copying the object.
(((copying)))

You can use this to store non-strings in a database. 

[TIP]
====
In fact, the storage of non-string in a database is so common that it has been encapsulated in a package called +JLD2+ (see https://github.com/JuliaIO/JLD2.jl).
====


=== Command Objects

Most operating systems provide a command-line interface, also known as a _shell_. Shells usually provide commands to navigate the file system and launch applications. For example, in Unix you can change directories with +cd+, display the contents of a directory with +ls+, and launch a web browser by typing (for example) +firefox+.
(((shell)))(((cd)))

Any program that you can launch from the shell can also be launched from Julia using a _command object_:
(((command object)))(((backticks)))((("``", see="backticks")))

[source,@julia-repl-test chap14]
----
julia> cmd = `echo hello`
`echo hello`
----

Backticks are used to delimit the command.

The function +run+ executes the command:
(((run)))((("function", "Base", "run", see="run"))))

[source,@julia-repl-test chap14]
----
julia> run(cmd);
hello
----

The +hello+ is the output of the echo command, sent to +STDOUT+. The +run+ function itself returns a process object, and throws an +ErrorException+ if the external command fails to run successfully.

If you want to read the output of the external command, +read+ can be used instead:
(((read)))

[source,@julia-repl-test chap14]
----
julia> a = read(cmd, String)
"hello\n"
----

For example, most Unix systems provide a command called +md5sum+ or +md5+ that reads the contents of a file and computes a “checksum”. You can read about MD5 at https://en.wikipedia.org/wiki/Md5. This command provides an efficient way to check whether two files have the same contents. The probability that different contents yield the same checksum is very small (that is, unlikely to happen before the universe collapses).
(((checksum)))(((md5)))(((md5sum)))

You can use a command object to run +md5+ from Julia and get the result:

[source,@julia-repl]
----
filename = "output.txt"
cmd = `md5 $filename`
res = read(cmd, String)
----


=== Modules

Suppose you have a file named +"wc.jl"+ with the following code:
(((linecount)))((("function", "programmer-defined", "linecount", see="linecount")))(((eachline)))

[source,julia]
----
function linecount(filename)
    count = 0
    for line in eachline(filename)
        count += 1
    end
    count
end

print(linecount("wc.jl"))
----

[source,@julia-eval]
----
file = open("wc.jl", "w")
print(file, """function linecount(filename)
    count = 0
    for line in eachline(filename)
        count += 1
    end
    count
end

print(linecount("wc.jl"))""")
close(file)
----

If you run this program, it reads itself and prints the number of lines in the file, which is 9. You can also include it in the REPL like this:
(((include)))((("function", "Base", "include", see="include")))

[source,jl-con]
----
julia> include("wc.jl")
9
----

Julia introduces modules to create separate variable workspace, i.e. new global scopes. 

A module starts with the keyword +module+ and ends with +end+. Naming conflicts are avoided between your own top-level definitions and those found in somebody else's code. +import+ allows to control which names from other modules are visible and +export+ specifies which of your names are public, i.e. can be used outside the module without being prefixed with the name of the module. 
(((scope)))(((module)))((("keyword", "module", see="module")))(((end)))(((import)))((("keyword", "import", see="import")))(((export)))((("keyword", "export", see="export")))(((LineCount)))((("module", "LineCount", see="LineCount")))((("function", "LineCount", "linecount", see="linecount")))

[source,julia]
----
module LineCount
    export linecount

    function linecount(filename)
        count = 0
        for line in eachline(filename)
            count += 1
        end
        count
    end
end
----

The module +LineCount+ object provides +linecount+:

[source,julia]
----
julia> using LineCount

julia> linecount("wc.jl")
11
----

==== Exercise 14-1

Type this example into a file named _wc.jl_, include it into the REPL and enter +using LineCount+.


[WARNING]
====
If you import a module that has already been imported, Julia does nothing. It does not re-read the file, even if it has changed.

If you want to reload a module, you have to restart the REPL. A package +Revise+ exists that can keep your sessions running longer (see https://github.com/timholy/Revise.jl).
====


=== Debugging

When you are reading and writing files, you might run into problems with whitespace. These errors can be hard to debug because spaces, tabs and newlines are normally invisible:
(((\n)))(((\t)))

[source,jlcon]
----
julia> s = "1 2\t 3\n 4";

julia> println(s)
1 2     3
 4
----

The built-in functions +repr+ or +dump+ can help. It takes any object as an argument and returns a string representation of the object.

(((repr)))((("function", "Base", "repr", see="repr")))(((dump)))((("function", "Base", "dump", see="dump")))

[source,@julia-eval chap14]
----
s = "1 2\t 3\n 4";
----

[source,@julia-repl-test chap14]
----
julia> repr(s)
"\"1 2\\t 3\\n 4\""
julia> dump(s)
String "1 2\t 3\n 4"
----

This can be helpful for debugging.
(((debugging)))

One other problem you might run into is that different systems use different characters to indicate the end of a line. Some systems use a newline, represented +\n+. Others use a return character, represented +\r+. Some use both. If you move files between different systems, these inconsistencies can cause problems.
(((\r)))

For most systems, there are applications to convert from one format to another. You can find them (and read more about this issue) at https://en.wikipedia.org/wiki/Newline. Or, of course, you could write one yourself.


=== Glossary

persistent::
Pertaining to a program that runs indefinitely and keeps at least some of its data in permanent storage.
(((persistent)))

text file::
A sequence of characters stored in permanent storage like a hard drive.
(((text file)))

directory::
A named collection of files, also called a folder.
(((directory)))

path::
A string that identifies a file.
(((path)))

relative path::
A path that starts from the current directory.
(((relative path)))

absolute path::
A path that starts from the topmost directory in the file system.
(((absolute path)))

catch::
To prevent an exception from terminating a program using the +try pass:[...] catch pass:[...] finally+ statements.
(((catching)))

database::
A file whose contents are organized like a dictionary with keys that correspond to values.
(((database)))

shell::
A program that allows users to type commands and then executes them by starting other programs.
(((shell)))

command object::
An object that represents a shell command, allowing a Julia program to run commands and read the results.
(((command object)))


=== Exercises

[[ex14-1]]
==== Exercise 14-2

Write a function called +sed+ that takes as arguments a pattern string, a replacement string, and two filenames; it should read the first file and write the contents into the second file (creating it if necessary). If the pattern string appears anywhere in the file, it should be replaced with the replacement string.
(((sed)))((("function", "programmer-defined", "sed", see="sed")))

If an error occurs while opening, reading, writing or closing files, your program should catch the exception, print an error message, and exit.

[[ex14-2]]
==== Exercise 14-3

If you have done <<ex12-2>>, you’ll see that a dictionary is created that maps from a sorted string of letters to the array of words that can be spelled with those letters. For example, +"opst"+ maps to the array +["opts", "post", "pots", "spot", "stop", "tops"]+.

Write a module that imports +anagramsets+ and provides two new functions: +storeanagrams+ should store the anagram dictionary using +JLD2+; +readanagrams+ should look up a word and return an array of its anagrams.
(((storeanagrams)))((("function", "programmer-defined", "storeanagrams", see="storeanagrams")))(((readanagrams)))((("function", "programmer-defined", "readanagrams", see="readanagrams")))

[[ex14-3]]
==== Exercise 14-4

In a large collection of MP3 files, there may be more than one copy of the same song, stored in different directories or with different file names. The goal of this exercise is to search for duplicates.

. Write a program that searches a directory and all of its subdirectories, recursively, and returns an array of complete paths for all files with a given suffix (like _.mp3_).

. To recognize duplicates, you can use +md5sum+ or +md5+ to compute a “checksum” for each files. If two files have the same checksum, they probably have the same contents.

. To double-check, you can use the Unix command +diff+.

